#resource "timing.h"
#hostcode-c "#include \"timing.h\""

@ClockUseSD

scchart Pacemaker {
  input signal AS, VS
  output signal AP, VP
  
    region Atrium {
      // Define clock variables
      clock VentricleTimer, AtriumTimer
      
      initial state WaitAS
        // If a pulse is detected after the PVARP timer has expired
        if (VentricleTimer >= `PVARP_VALUE`) && AS go to setupVS

        // If the AEI timer has expired and the AVI timer has expired generate an Atrium Pulse
        if (VentricleTimer >= `AEI_VALUE`) do AP go to setupVS
      
      connector state setupVS
        // Reset Timers
        immediate do AtriumTimer = 0 go to WaitVS
        
      state WaitVS
        // If a pulse is detected after the VRP timer has expired 
        if (VentricleTimer >= `VRP_VALUE`) && VS go to setupAS
        
        // If the URI timer has expired and the AVI timer has expired generate a Ventricle Pulse
        if (VentricleTimer >= `URI_VALUE`) && (AtriumTimer >= `AVI_VALUE`) do VP go to setupAS
        
        // If the LRI timer has expited generate a Ventricle Pulse
        if (VentricleTimer >= `LRI_VALUE`) do VP go to setupAS
      
        connector state setupAS
          // Reset Timers
          immediate do VentricleTimer = 0 go to WaitAS
    }
}
